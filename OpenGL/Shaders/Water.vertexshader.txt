#version 330

in vec3 vertices;
in vec3 normals;
in vec2 texCoords;
in vec3 tangents;
in vec3 bitangents;
layout (location=10) in mat4 instanceMatrix;

uniform vec3 CameraPosition;
uniform mat4 World;
uniform mat4 WVP;
uniform bool EnableInstancing = false;
uniform float Time;
uniform float Frequency = 4.0;
uniform float Amplitude = 0.01;

out vec3 retNormal;
out vec2 retTexCoord;
out vec3 retFragPos;
out vec3 retViewDirection;
out mat3 retTBN;

void main()
{
    vec3 pos = vertices;
    
    // Apply wave animation to Y position
    pos.y += sin(pos.x * Frequency + Time) * Amplitude;
    pos.y += sin(pos.z * Frequency + Time * 0.7) * Amplitude * 0.8;
    
    mat4 world = World;
    if (EnableInstancing)
    {
        world *= instanceMatrix;
        gl_Position = WVP * instanceMatrix * vec4(pos, 1.0);
    }
    else
    {
        gl_Position = WVP * vec4(pos, 1.0);
    }
    
    retNormal = normalize(mat3(transpose(inverse(world))) * normals);
    retTexCoord = texCoords;
    retFragPos = vec3(world * vec4(pos, 1.0f));
    retViewDirection = normalize(CameraPosition - retFragPos);
    
    // Transform tangent, bitangent, and normal to world space
    vec3 T = normalize(vec3(world * vec4(tangents, 0.0)));
    vec3 B = normalize(vec3(world * vec4(bitangents, 0.0)));
    vec3 N = normalize(vec3(world * vec4(normals, 0.0)));
    
    // Gram-Schmidt orthogonalization: make tangent perpendicular to normal
    T = normalize(T - dot(T, N) * N);
    
    // Recalculate bitangent to ensure it's perpendicular to both N and T
    B = cross(N, T);
    
    // Build TBN matrix (note: row-major order in GLM)
    retTBN = mat3(T, B, N);
}

